// Top-level build file where you can add configuration options common to all sub-projects/modules.
import java.text.SimpleDateFormat

ext.globalProjectName = 'WatchDesigner'

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
    }
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'com.github.ben-manes.versions'

def gitSha1() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def buildDate() {
    def dateFormat = new SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm\'Z\'')
    dateFormat.setTimeZone(TimeZone.getTimeZone('UTC'))
    return dateFormat.format(new Date())
}

// Build properties
ext.buildProperties = new Properties()
buildProperties.load(new FileInputStream(file('build.properties')))

// Build number
ext.buildNumberFile = file('build.number')
ext.buildNumberProperties = new Properties()
buildNumberProperties.load(new FileInputStream(buildNumberFile))
ext.buildNumber = buildNumberProperties['build.number'].toInteger()

// Versions used across modules
ext.buildToolsVersion = '23.0.3'
ext.supportLibVersion = '23.4.0'
ext.playServicesVersion = '8.4.0'
